import csdbc_mysql
import regex
import codec.json as json
package db_connector

var csdbc_adpter = null 
function start()
    # 重新绑定函数
    var error = system.out.println 
    var connect = "login_config.json"
    var correct = true
    var ifs = iostream.ifstream(connect)
    var cfg = null
    try 
        # 测试文件是否符合json规范
        cfg = json.to_var(json.from_stream(ifs))
    catch e1
        error("【ERROR】login_config.json文件未找到或有误！")
        system.exit(1)
    end
    if cfg.exist("server") && cfg.exist("port") && cfg.exist("password") && cfg.exist("user")
        # 数据库连接字符串
        var cnt_str = "Driver={MariaDB ODBC 3.0 Driver};Server=" + cfg.server+ ";Port=" + cfg.port + ";Database=scupm;User="+cfg.user+";Password="+cfg.password+";Option=3";
        try
            csdbc_adpter = csdbc_mysql.connect_str(cnt_str)
        catch e
            ########### csdbc异常处理 ##############骂就骂老李，异常处理还得用正则表达式
            # system.out.println(e.what)
            # 检查访问错误，访问拒绝。
            var reg=regex.build("Access denied")
            var res = reg.search(e.what)
            if !res.empty()
                error("【ERROR】访问权限错误，请检查账户或密码")
            end
            # 检查连接超时，hy00是对应的错误码，通过正则表达式检索
            # 后面可能会更新更细的异常信息，可以直接catch
            reg = regex.build("HY00")
            res = reg.search(e.what)
            if !res.empty()
                error("【ERROR】访问超时！请检查目标服务器地址或服务器运行情况！")
            end
            reg = regex.build("IM00")
            res = reg.search(e.what)
            if !res.empty()
                system.out.println("【ERROR】您未安装数据库驱动程序！！！\n请在该链接下安装：https://downloads.mariadb.com/Connectors/odbc/connector-odbc-3.0.2/")
            end
            system.exit(1)
            ########## 异常处理结束 ##############
        end
        return csdbc_adpter
    else
        error("数据读取异常，请检查工作路径是否正确，以及路径下是否有./login_config.json文件，以及该文件是否配置正确,四个字段为：\n\"server\":\n\"port\":\n\"user\":\n\"password\":")
    end
end
